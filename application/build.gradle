buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE"
    }
}

allprojects {

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
    }

    sourceSets {
        main {
            java { srcDirs = [ 'main' ] }
            resources { srcDir 'resources' }
        }
        test {
            java { srcDirs = [ 'test' ] }
        }
    }

    task view_paths {
        doLast { task ->
            println "$task.project.name"
            println "------------------"
            println "Main: $sourceSets.main.java.srcDirTrees"
            println "   Resources: $sourceSets.main.resources.srcDirTrees"
            println "Tests: $sourceSets.test.java.srcDirTrees"
            println "   Resources: $sourceSets.test.resources.srcDirTrees"
        }
    }

    task hello {
        doLast { task ->
            println "I'm $task.project.name"
        }
    }
}

subprojects {

    apply plugin: 'jacoco'

    group = "pe.lacafetalab.${rootProject.name}"

    dependencies {
        // Prod
        compile 'org.springframework.data:spring-data-jpa:2.1.9.RELEASE'
        compile 'javax.persistence:javax.persistence-api:2.2'
        compile 'javax.transaction:javax.transaction-api:1.3'
        compile 'org.springframework:spring-context'
        compile 'org.hibernate:hibernate-core:5.4.4.Final'
        compile 'com.vladmihalcea:hibernate-types-52:2.5.0'

        compile 'io.projectreactor:reactor-bus:2.0.8.RELEASE'
        compile 'io.projectreactor:reactor-core:2.0.8.RELEASE'

        compileOnly 'org.projectlombok:lombok:1.18.8'
        annotationProcessor 'org.projectlombok:lombok:1.18.8'

        compile 'org.apache.commons:commons-lang3:3.9'
        compile 'org.apache.logging.log4j:log4j-core:2.12.0'

        // Test
        testCompile 'org.springframework.boot:spring-boot-starter-test:2.1.6.RELEASE'

        if (project.name != "applications") {
            testCompile project(":applications").sourceSets.main.output
        }

        if (project.name != "sharedtest") {
            testCompile project(":src:sharedtest")
        }

        if (project.name != "shared") {
            compile project(":src:shared")
        }


    }

    test {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat "full"

            debug {
                events "started", "skipped", "failed"
                exceptionFormat "full"
            }
        }

        reports {
            html.enabled = true
        }
    }

    jacoco {
        toolVersion = "0.8.4"
        reportsDir = file("$buildDir/customJacocoReportDir")
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination file("${buildDir}/jacocoHtml")
        }

        //afterEvaluate {
        //    classDirectories = files(classDirectories.files.collect {
        //    	fileTree(dir: it, exclude: [
        //			'**/*repository**'
        //        ])
        //    })
        //}
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.0
                }
            }
        }
    }
    check.dependsOn jacocoTestCoverageVerification
}